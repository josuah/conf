#!/usr/bin/awk -f


## utilities ##

function die(msg)
{
	print "error: " msg >"/dev/stderr"
	exit(1)
}

function assert(expr, fn)
{
	if (!expr)
		die(fn ": assert failed")
}

function join(array, sep,
	i, str)
{
	for (i = 1; i in array; i++)
		str = (i == 1 ? array[i] : str sep array[i])
	return str
}

function swap(array, a, b,
	tmp)
{
	tmp = array[a]
	array[a] = array[b]
	array[b] = tmp
}

function qsort(array, beg, end)
{
	if (beg >= end)					# end recursion
		return

	a = beg + 1					# 1st is the pivot
	b = end
	while (a < b) {
		while (a < b && array[a] <= array[beg])	# beg: skip lesser
			a++
		while (a < b && array[b] > array[beg])	# end: skip greater
			b--
		swap(array, a, b)			# found 2 misplaced
	}

	if (array[beg] > array[a])			# put the pivot back
		swap(array, beg, a)

	qsort(array, beg, a - 1)			# sort top half
	qsort(array, a, end)				# sort bottom half
}

function delete_prefix(array, prefix,
	x)
{
	for (x in array)
		if (substr(x, 1, length(prefix)) == prefix)
			delete array[x]
}

function array_length(array,
	x, n)
{
	for (x in array)
		n++
	return n
}

function shift(array,
	first, i)
{
	first = array[1]
	for (i = 1; i + 1 in array; i++)
		array[i] = array[i + 1]
	delete array[i]
	return first
}
 
function copy(dst, src,
	x)
{
	for (x in src)
		dst[x] = src[x]
}

function color(n)
{
	return (ENVIRON["NCOLOR"]) ? "" : sprintf("\033[%dm", n)
}


## database ##

function db_clean(line)
{
	gsub("[ \t]+", " ", line)
	sub("^ *", "", line)
	sub(" *$", "", line)
	return line
}

function db_split(array, keys, row, prefix,
	i, n, key, eq, val)
{
	for (i = 1; i in array; i++) {
		eq = index(array[i], "=")
		if (eq == 0) {
			keys[++n] = array[i]
		} else {
			key = substr(array[i], 1, eq - 1)
			val = substr(array[i], eq + 1)
			row[prefix key] = val
		}
	}
}

function db_getline(path, prefix, row,
	array, _, line, n)
{
	if ((getline line <path) <= 0) {
		close(path)
		return 0
	}
	if (line ~ /^[ \t]*#/)
		return db_getline(path, prefix, row)

	n = row[""]
	split(db_clean(line), array, " ")
	db_split(array, _, row, prefix)
	row[""] = n + 1
	return 1
}

function db_match(query, row, prefix,
	array, rowq, x, _)
{
	db_split(query, _, qrow)
	for (x in qrow) {
		print x "=" qrow[x]
		if (!(prefix x in row) || row[prefix x] != qrow[x])
			return 0
	}
	return 1
}

function db_getmatch(path, query, row, prefix)
{
	while (db_getline(path, prefix, row))
		if (db_match(query, row, prefix))
			return 1
	return 0
}

function db_expand(str, vars,
	x)
{
	for (x in vars)
		gsub("\\[" x "\\]", vars[x], str)
	return str
}

function db_query(path, keys, query, result, prefix,
	i, x, row, n)
{
	split("", result)
	if (db_getmatch(path, query, row, prefix) == 0)
		return 0
	for (i = 1; i in keys; i++)
		result[++n] = row[keys[i]]
	return 1
}

function db_sort(row, keys,
	x, n)
{
	for (x in row)
		keys[++n] = x
	qsort(keys, 1, n)
	return n
}

function db_pprint(row, width,
	i, n, put, len, col, keys)
{
	printf("%3d ", row[""])
	col = 4

	n = db_sort(row, keys)

	for (i = 2; i <= n; i++) {
		len = 1 + length(keys[i]) + 1 + length(row[keys[i]])
		if (col + len > width && col > 4) {
			printf "\n    "
			col = 4
		}
		printf " " color(36) "%s=" color(0) "%s", keys[i], row[keys[i]]
		col += len
	}

	printf "\n"
}


## templates ##

function tpl_replace(text, vars,
	head, tail, query, result, value, n)
{
	tail = text
	while (match(tail, "\\{\\{[^}]+\\}\\}")) {
		query = substr(tail, RSTART + 2, RLENGTH - 4)
		sub("^ *", "", query)
		sub(" *$", "", query)
		query = db_expand(query, vars)

		if (query in G) {
			value = VAR[query]
		} else if (query in vars) {
			value = vars[query]
		} else {
			n = tpl_query(query, result, prefix)

			if (n == 0)
				die("no result for " query)
			for (n in result)
				break
			value = result[n]
		}

		head = head substr(tail, 1, RSTART - 1) value
		tail = substr(tail, RSTART + RLENGTH)
	}
	return head tail
}

function tpl_inc_tail(lv,
	n, array)
{
	n = split(lv, array, ".")
	array[n]++
	return join(array, ".")
}

function tpl_inc_level(lv,
	n, array)
{
	n = split(lv, array, ".")
	array[n + 1] = 1
	return join(array, ".")
}

function tpl_dec_level(lv,
	n, array)
{
	n = split(lv, array, ".")
	delete array[n]
	return join(array, ".")
}

function tpl_query(strq, prefix, row,
	n, path, query)
{
	sub(":", " ", strq)
	n = split(strq, query, " ")
	assert(n >= 2, "tpl_query")
	path = shift(query)
	return db_getmatch(path, query, row, prefix)
}

function tpl_flush(texts, queries, lv, vars,
	n, x, array)
{
	if (!(lv in texts))
		return

	n = split(lv, array, ".")
	while (tpl_query(db_expand(queries[lv], vars), n ":", array)) {
		copy(vars, array)
		for (x = lv; x in texts; x = tpl_inc_tail(x)) {
			printf "%s", tpl_replace(texts[x], vars)
			tpl_flush(texts, queries, tpl_inc_level(x), vars)
		}
	}
	delete_prefix(vars, n ":")
}


## commands ##

function cmd_template(argv,
	path, line, lv, queries, texts)
{
	if (!(1 in argv) || (2 in argv))
		usage()

	VAR["path"] = path = argv[1]

	while (getline line <path) {

		if (sub(/^#+for /, "", line)) {
			lv = tpl_inc_level(lv)
			queries[lv] = line

		} else if (line ~ /^#+end/) {
			if (lv == "")
				die("more #end than #for")
			lv = tpl_dec_level(lv)
			lv = tpl_inc_tail(lv)

			if (lv == "") {
				tpl_flush(texts, queries, "1")
				split("", queries)
				split("", texts)
			}

		} else if (lv == "") {
			print tpl_replace(line)

		} else {
			texts[lv] = texts[lv] line "\n"
		}
	}
}

function cmd_get(path, argv,
	keys, query, result, i, n)
{
	if (!(1 in argv))
		usage()

	db_split(argv, keys, query)
	while (db_query(path, keys, query, result)) {
		for (i = 1; i in result; i++)
			printf "%s%s", (i == 1 ? "" : "\t"), result[i]
		printf "\n"
	}
}

function cmd_list(path, argv,
	cmd, query, row, width)
{
	cmd = "stty size 2>/dev/null"
	cmd | getline width
	close(cmd)
	sub(".* ", "", width)
	width = (width == "" ? 80 : width + 0)

	printf "\n"
	while (db_getmatch(path, argv, row)) {
		db_pprint(row, width)
		printf "\n"
	}
}

function cmd_add(path, argv,
	i, out, keys)
{
	if (!(1 in argv))
		usage()

	qsort(argv, 1, array_length(argv))

	if (system("test -f '" path "'") != 0)
		die("could not find table file " path)

	for (i = 1; i in keys; i++)
		printf "%s%s", (i == 1 ? "" : " "), argv[keys[i]] >>path
	printf "\n" >>path
	fflush(path)
}

function cmd_set(path, argv,
	tmp, num, i, keys, row)
{
	if (!(3 in argv))
		usage()

	qsort(argv, 1, array_length(argv))
	split(shift(argv), num, ",")
	db_split(argv, keys, row)

	db_getnum()
}


## main ##

function usage()
{
	print "usage:"						>"/dev/stderr"
	print " adm-db <db> list [<key>=<value>...]"		>"/dev/stderr"
	print " adm-db <db> get <key> [<key>=<value>...]"	>"/dev/stderr"
	print " adm-db <db> set <n> <key>=<value>..."		>"/dev/stderr"
	print " adm-db <db> add <key>=<value>..."		>"/dev/stderr"
	print " adm-db <db> move <n1> <n2>"			>"/dev/stderr"
	print " adm-db <template> template"			>"/dev/stderr"
	exit(1)
}

BEGIN {
	cmd = "date +%s"
	cmd | getline VAR["now"]
	close(cmd)

	cmd = "hostname -s"
	cmd | getline VAR["host"]
	close(cmd)

	delete ARGV[0]
	path = shift(ARGV)
	cmd = shift(ARGV)

	if (cmd == "list") {
		cmd_list(path, ARGV)

	} else if (cmd == "get") {
		cmd_get(path, ARGV)

	} else if (cmd == "set") {
		cmd_set(path, ARGV)

	} else if (cmd == "move") {
		cmd_move(path, ARGV)

	} else if (cmd == "template") {
		cmd_template(path, ARGV)

	} else {
		usage()
	}
}
