#!/usr/bin/awk -f
# build a table out of rcctl output

function table() {
	while (("exec rcctl ls started" | getline) > 0)
		sv[$0]++
	ignore["pf"] = ignore["check_quotas"] = ignore["library_aslr"] = 1
	while (("exec rcctl ls on" | getline) > 0) {
		if ($0 in ignore) continue
		printf("%s %s\n", $0, ($0 in sv) ? "ok" : "err")
	}
}

BEGIN {
	printf("%-20s", "host")
	for (i = 1; i in ARGV; i++) {
		gsub("[^-@._A-Za-z0-9]", "", ARGV[i])
		cmd = "exec ssh "ARGV[i]" exec sh /dev/stdin list <"self
		while ((cmd | getline) > 0) {
			stat[ARGV[i]" "$1] = $2;
			if (!uniq[$1]++) sv[ns++] = $1
		}
		printf "%-"length(ARGV[i])"s ", ARGV[i]
	}
	printf "\n"

	for (is = 0; is < ns; is++) {
		printf "%-20s", sv[is]
		for (i = 1; i in ARGV; i++) {
			s = stat[ARGV[i]" "sv[is]]
			printf " %-"length(ARGV[i])"s", s ? s : "-"
		}
		printf "\n"
	}
}
