#!/usr/bin/awk -f

function replace(str, this,
	head, tail, key, value, path)
{
	tail = str
	head = ""
	while (match(tail, "\\{\\{[^}]+\\}\\}")) {
		key = substr(tail, RSTART + 2, RLENGTH - 4)
		gsub("%", this, key)
		sub("^ *", "", key)
		sub(" *$", "", key)

		if (key == "this") {
			value = this
		} else if (key == "now") {
			value = global["now"]
		} else {
			path = global["path"] "/" this "/" parts[2]
			getline value <path
		}

		head = head substr(tail, 1, RSTART - 1) value
		tail = substr(tail, RSTART + RLENGTH)
	}
	return head tail
}

function read_block(\
	str, line)
{
	for (str = ""; getline line && line !~ /^#end/; str = str line "\n")
		continue
	return str
}

function list_dir(path, table,
	cmd, i)
{
	cmd = "ls '" path "'"
	for (i = 1; cmd | getline > 0; i++)
		table[i] = $0
	close(cmd)
}

function print_block(str, table,
	i)
{
	for (i in table)
		printf "%s", replace(str, table[i])
}

BEGIN {
	cmd = "date +'%Y/%m/%d %H:%M:%S %z'"
	cmd | getline global["now"]
	close(cmd)

	global["path"] = ARGV[1]
	for (i = 1; i < ARGC; i++)
		ARGV[i] = ARGV[i + 1]
	ARGC--
}

/^#for / {
	list_dir($2, table)
	print_block(read_block(), table)
	delete table
	next
}

{
	print replace($0)
}
