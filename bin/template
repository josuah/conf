#!/usr/bin/awk -f

function esctxt(txt)
{
	gsub(/'/, "'\''", txt)
	return txt
}

function escvar(var)
{
	sub(/.*/, "'\"$&\"'", var)
	return var
}

function shell(cmd)
{
	if("DEBUG" in ENVIRON)
		printf("%s", cmd)
	else
		printf("%s", cmd) | "exec sh -eu"
}

function enter(to, was)
{
	old = zone
	zone = to
	return to != old
}

NR == 1 {
	enter("code")
	shell("file='"FILENAME"'\n")
	shell("date=$(date +%Y-%m-%d)\n")
}

{
	if(sub(/^%% ?/, "")){
		if(enter("code")) shell("'\n")
	}else{
		$0 = esctxt($0)
		if(enter("text")) shell("echo -n '")
	}
	while(match($0, /%%[-_:0-9a-zA-Z]+%%/)){
		shell(substr($0, 1, RSTART-1))
		shell(escvar(substr($0, RSTART+2, RLENGTH-4)))
		$0 = substr($0, RSTART + RLENGTH)
	}
	shell($0"\n")
}

END {
	if(enter("code")) shell("'\n")
}
