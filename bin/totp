#!/usr/bin/awk -f
# totp password store using pass(1) like https://pypi.org/project/totp/

function rshift_(x, n) {
	return int(x / 2^n)
}

#  byte0       byte1       byte2      byte3       byte4
#  |.....|...  ..|.....|.  ....|....  .|.....|..  ...|.....|
#  b0    b1      b2    b3      b4      b5    b6      b7

function b32encode(bin,
	n, i)
{
	for (i = 0; i < length(bin); n = 0) {
		do n = n * 256 + ASCII[substr(bin, i + 1, 1)]
		while (++i % 5)

		b32 = b32 \
		 B32ENCODE[rshift_(n, 35) % 32 + 1] \
		 B32ENCODE[rshift_(n, 30) % 32 + 1] \
		 B32ENCODE[rshift_(n, 25) % 32 + 1] \
		 B32ENCODE[rshift_(n, 20) % 32 + 1] \
		 B32ENCODE[rshift_(n, 15) % 32 + 1] \
		 B32ENCODE[rshift_(n, 10) % 32 + 1] \
		 B32ENCODE[rshift_(n, 5) % 32 + 1] \
		 B32ENCODE[n % 32 + 1]
	}
	i = length(bin) % 5
	if (i == 1) sub(/......$/, "======", b32)
	if (i == 2) sub(/....$/, "====", b32)
	if (i == 3) sub(/...$/, "===", b32)
	if (i == 4) sub(/.$/, "=", b32)
	return b32
}

#  b0     b1     b2    b3     b4     b5    b6     b7
#  |..... ...|.. ..... .|.... ....|. ..... ..|... .....|
#  byte0     byte1      byte2     byte3      byte4

function b32hex(b32,
	n, x)
{
	for (i = 0; i < length(b32); n = 0) {
		do n = n * 32 + B32DECODE[substr(b32, i + 1, 1)]
		while (++i % 8)
		x = x sprintf("%08x", n)
	}
	while(sub(/=$/, "", b32))
		sub(/..$/, "", x)
	return x
}

function hex(x,
	i, n)
{
	for (i = 1; i <= length(x); i++)
		n = n * 16 + index("0123456789abcdef", substr(x, i, 1)) - 1
	return n
}

function totp(key,
	t)
{
	# time/30 encoded as '\x00\x00\x00\x00\x00\x00'
	"exec date +%s" | getline NOW
	t = sprintf("%016x", int(NOW / 30))
	gsub(/../, "\\x&", t)

	# hmac the time with the key
	"printf '"t"' | openssl sha1 -mac HMAC -macopt hexkey:"key | getline
	key = substr($2, hex(substr($2, 40, 1)) * 2 + 1, 8)

	# extract an integer from the digest
	key = sprintf("%x%s", hex(substr(key, 1, 1)) % 8, substr(key, 2))
	return sprintf("%06d", hex(key) % 1000000)
}

function usage()
{
	print"usage: totp [add] service" >"/dev/stderr"
	exit(1)
}

BEGIN {
	for (i = 0; i < 256; i++) ASCII[sprintf("%c", i)] = i
	split("A B C D E F G H I J K L M N O P Q R S T U V W X Y Z 2 3 4 5 6 7", x)
	for (i in x) B32ENCODE[i - 1] = x[i]
	for (i in x) B32DECODE[x[i]] = i - 1

	if (ARGV[1] == "add" && ARGC == 3) {
		printf "Enter shared key: " >"/dev/stderr"
		exit(system("exec pass insert '2fa/"ARGV[2]"/code'"))
	} else if (ARGC == 2) {
		"exec pass show '2fa/"ARGV[1]"/code'" | getline b32
		if (b32 == "") exit(1)
		print totp(b32hex(b32))
	} else {
		usage()
	}
}
